apply plugin: 'com.android.application'
def ABI_FILTER = rootProject.ext.ABI_FILTER

android {
    compileSdkVersion = gradle.sdkVersion
    defaultConfig {
        applicationId = 'com.osgverse.demoapp'
        minSdkVersion gradle.minSdkVersion
        targetSdkVersion gradle.targetSdkVersion
        ndk { abiFilters ABI_FILTER }

        externalNativeBuild {
            cmake {
                cppFlags "-frtti -fexceptions"
                abiFilters ABI_FILTER
            }
        }
    }
    signingConfigs {
        release {
            storeFile file("keystore-path")
            storePassword "keystore-pass"
            keyAlias "alias"
            keyPassword "alias-password"

            // Optional, specify signing versions used
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    buildTypes {
        debug {
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_BUILD_TYPE=Debug"
                }
            }
        }
        release {
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_BUILD_TYPE=Release"
                }
            }
            minifyEnabled = false
            proguardFiles.add(file('proguard-rules.txt'))
            signingConfig signingConfigs.release
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs += '../../build/sdl2_android/lib'
        }
        debug {
            jniLibs.srcDirs += '../../build/sdl2_android/lib/Debug'
        }
        release {
            jniLibs.srcDirs += '../../build/sdl2_android/lib/Release'
        }
    }
    externalNativeBuild {
        cmake {
            path "../main/CMakeLists.txt"
        }
    }
}

gradle.buildFinished {
    fileTree(".cxx").visit { details ->
        if (details.directory && details.file.listFiles()?.length == 0) {
            details.file.deleteDir()
        }
    }
}

preBuild.dependsOn ":sdl2:build", ":osgverse:build"
//preBuild.dependsOn ":sdl2:build"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.so'])
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation 'com.android.support:appcompat-v7:28.0.0'
}
